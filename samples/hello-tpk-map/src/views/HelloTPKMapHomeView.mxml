<?xml version="1.0" encoding="utf-8"?>
<!--
///////////////////////////////////////////////////////////////////////////
// Copyright (c) 2010-2013 Esri. All Rights Reserved.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//    http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
///////////////////////////////////////////////////////////////////////////
-->
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:s="library://ns.adobe.com/flex/spark"
        xmlns:esri="http://www.esri.com/2008/ags"
        xmlns:components="com.github.lheberlie.components.*"
        actionBarVisible="false"
        backgroundAlpha="0"
        creationComplete="helloTPKMapView_creationCompleteHandler(event)"
        initialize="helloTPKMapView_initializeHandler(event)"
        viewActivate="helloTPKMapView_viewActivateHandler(event)">

    <fx:Script>
        <![CDATA[
            import com.esri.ags.events.ExtentEvent;
            import com.esri.ags.events.LayerEvent;
            import com.esri.ags.events.MapEvent;

            import mx.collections.ArrayCollection;
            import mx.events.FlexEvent;

            import spark.events.ViewNavigatorEvent;
            [Bindable]
            private var baseMapTPKFile:File;
            private var platform:String;

            [Bindable]
            private var lifeCycleArrayCollection:ArrayCollection = new ArrayCollection();

            protected function helloTPKMapView_initializeHandler(event:FlexEvent):void
            {
                lifeCycleArrayCollection.addItem("View Initialized");
                //IOS 11,6,602,167
                var versionString:String = Capabilities.version;
                var pattern:RegExp = /^(\w*) (\d*),(\d*),(\d*),(\d*)$/;
                var result:Object = pattern.exec(versionString);
                if (result != null)
                {
                    /*
                    trace("input: " + result.input);
                    trace("platform: " + result[1]);
                    trace("majorVersion: " + result[2]);
                    trace("minorVersion: " + result[3]);
                    trace("buildNumber: " + result[4]);
                    trace("internalBuildNumber: " + result[5]);
                    */
                    platform = result[1];
                }
                else
                {
                    trace("Unable to match RegExp.");
                }

                if (platform == "IOS")
                {
                    /*
                     *  If someone has copied a file named BaseMap.tpk into the documents directory
                     *  through iTunes then use that tpk instead of the one that we include in the
                     *  assets/tpk folder.
                    */
                    // If you inspect the app inside of XCode's Organizer you can see the "Documents" folder in the "Data files in Sandbox"
                    // example: file:///var/mobile/Applications/BBC56080-AC4F-44B7-9B37-B3BF776523F0/Documents/BaseMap.tpk
                    baseMapTPKFile = File.documentsDirectory.resolvePath("BaseMap.tpk");
                    if (!baseMapTPKFile.exists)
                    {
                        trace("BaseMap.tpk was not found in the Documents directory");
                        baseMapTPKFile = File.applicationDirectory.resolvePath("assets/tpk/World.tpk");
                    }
                    else
                    {
                        trace("basemapTPKFile.exists: " + baseMapTPKFile.exists);
                        trace("basemapTPKFile.nativePath: " + baseMapTPKFile.nativePath);
                        trace("basemapTPKFile.url: " + baseMapTPKFile.url);
                    }
                }
                else if (platform == "AND")
                {
                    //TODO: need to fix/add condition for Android
                    //LH: I don't have any Android devices so I couldn't test this.
                    var file:String = File.applicationDirectory.url;
                    baseMapTPKFile = File.applicationDirectory.resolvePath("assets/tpk/World.tpk");
                }
            }

            protected function helloTPKMapView_creationCompleteHandler(event:FlexEvent):void
            {
                lifeCycleArrayCollection.addItem("View Creation Complete");
            }

            protected function helloTPKMapView_viewActivateHandler(event:ViewNavigatorEvent):void
            {
                lifeCycleArrayCollection.addItem("View Activated");
                helloWorldLabel.visible = false;
            }

            protected function map_loadHandler(event:MapEvent):void
            {
                lifeCycleArrayCollection.addItem("Map loaded");
                trace("basemapLocalTiledLayer.loaded: " + basemapLocalTiledLayer.loaded);
            }

            protected function basemapLocalTiledLayer_loadHandler(event:LayerEvent):void
            {
                lifeCycleArrayCollection.addItem("BaseMap loaded");
                map.extent = basemapLocalTiledLayer.initialExtent;
            }

            protected function map_extentChangeHandler(event:ExtentEvent):void
            {
                mapScale.text = "1:" + numberFormatter.format(map.scale);
            }

            protected function map_creationCompleteHandler(event:FlexEvent):void
            {
                lifeCycleArrayCollection.addItem("map creation complete");
            }

            protected function basemapLocalTiledLayer_loadErrorHandler(event:LayerEvent):void
            {
                lifeCycleArrayCollection.addItem("BaseMap load error!");
                trace(event.fault.faultString);
            }
        ]]>
    </fx:Script>

    <fx:Declarations>
        <!-- Place non-visual elements (e.g., services, value objects) here -->
        <s:NumberFormatter id="numberFormatter"
                           fractionalDigits="0"
                           trailingZeros="false"/>
    </fx:Declarations>

    <s:Rect left="0" right="0" top="0" bottom="0">
        <s:fill>
            <s:LinearGradient x="200" y="0"
                              rotation="90"
                              scaleX="400">
                <s:GradientEntry alpha="1"
                                 color="#3e3e3e"
                                 ratio="0"/>
                <s:GradientEntry alpha="1"
                                 color="#161616"
                                 ratio="1"/>
            </s:LinearGradient>
        </s:fill>
    </s:Rect>

    <esri:Map id="map"
              creationComplete="map_creationCompleteHandler(event)"
              extentChange="map_extentChangeHandler(event)"
              load="map_loadHandler(event)"
              zoomSliderVisible="false">
        <esri:ArcGISLocalTiledLayer id="basemapLocalTiledLayer"
                                    file="{baseMapTPKFile}"
                                    load="basemapLocalTiledLayer_loadHandler(event)"
                                    loadError="basemapLocalTiledLayer_loadErrorHandler(event)"/>
    </esri:Map>

    <s:Label id="helloWorldLabel"
             horizontalCenter="0"
             styleName="helloWorldMapLabel"
             text="Hello World!"
             verticalCenter="0"/>

    <s:Label id="mapScale"
             left="20" bottom="50"
             styleName="mapScaleLabel"/>

    <components:MobileNavigation left="20" top="20"
                                 map="{map}"/>

    <s:Group width="300" height="300"
             right="20" top="20">
        <s:Rect left="0" right="0" top="0" bottom="0"
                radiusX="10"
                radiusY="10">
            <s:fill>
                <s:LinearGradient x="200" y="0"
                                  rotation="90"
                                  scaleX="400">
                    <s:GradientEntry alpha="1"
                                     color="#3e3e3e"
                                     ratio="0"/>
                    <s:GradientEntry alpha="1"
                                     color="#161616"
                                     ratio="1"/>
                </s:LinearGradient>
            </s:fill>
        </s:Rect>
        <s:Label height="30"
                 left="5" right="5" top="5"
                 styleName="lifeCycleListHeaderLabel"
                 text="Life-cycle events"/>
        <s:List left="5" right="5" top="40" bottom="5"
                dataProvider="{lifeCycleArrayCollection}"
                itemRenderer="spark.components.LabelItemRenderer"
                typicalItem="View Creation Complete"/>
    </s:Group>

</s:View>
